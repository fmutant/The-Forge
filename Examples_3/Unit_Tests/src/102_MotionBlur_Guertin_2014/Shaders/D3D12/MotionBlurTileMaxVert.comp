#define UNORM 1

RWTexture2D<min16float2> TileMaxVert: register(u0);
Texture2D<min16float2> TileMaxHorzTexture : register(t0);

cbuffer cbMotionBlurConsts : register(b3)
{
	uint4 consts;
	uint4 sizes;
}

min16float2 vmax(min16float2 s, min16float2 v)
{
#if UNORM
	s = s * min16float2(2.0h, 2.0h) - min16float2(1.0h, 1.0h);
#endif
	return dot(s, s) < dot(v, v) ? v : s;
}

[numthreads(4, 16, 1)]
void main(uint3 DTid : SV_DispatchThreadID)
{
	if (any(sizes.zw < DTid.xy)) return;

	uint3 xyl = uint3(DTid.x, DTid.y * consts.x, 0u);
	min16float2 tile_max = float2(0.0h, 0.0h);
	for (uint j = 0u; j < consts.x && xyl.y < sizes.y; ++j, ++xyl.y)
	{
		tile_max = vmax(TileMaxHorzTexture.Load(xyl), tile_max);
	}
#if UNORM
	tile_max = tile_max * min16float2(0.5h, 0.5h) + min16float2(0.5h, 0.5h);
#endif
	TileMaxVert[DTid.xy] = tile_max;
}
