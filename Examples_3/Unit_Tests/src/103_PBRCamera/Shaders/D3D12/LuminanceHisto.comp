cbuffer cbLuminanceMean : register(b0, UPDATE_FREQ_PER_FRAME)
{
	float mLuminanceMin;
	float mLuminanceRange;
	float mLuminanceRangeInv;
	float mFrameTime;
}

RWByteAddressBuffer HistoGlobal : register(u0);
Texture2D<min16float> LuminanceTexture : register(t0);

groupshared uint HistoLocal[128];

[numthreads(16, 8, 1)]
void main(uint3 tid : SV_DispatchThreadID, uint gidx : SV_GroupIndex, uint3 groupID : SV_GroupID)
{
	if (0 == groupID.x && 0 == groupID.y)
		HistoGlobal.Store(gidx << 2u, asuint(0u));

	HistoLocal[gidx] = 0;
	GroupMemoryBarrierWithGroupSync();

	min16float loglum = LuminanceTexture.Load(uint3(tid.xy, 0));
	min16float luminance_normalize = saturate((loglum - mLuminanceMin) * mLuminanceRangeInv);
	uint bin = uint(luminance_normalize * min16float(127.0h) + min16float(1.0h));

	InterlockedAdd(HistoLocal[bin], asuint(1u));

	GroupMemoryBarrierWithGroupSync();

	HistoGlobal.InterlockedAdd(gidx << 2u, asuint(HistoLocal[gidx]));
}